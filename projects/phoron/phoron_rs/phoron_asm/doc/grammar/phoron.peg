# Jasmin-compatible Phoron Assembly Format.
 
PhoronProgram <- line_comment* Header eof

Header <- SourceFileDef? (ClassDef /InterfaceDef)  SuperDef FieldDef* MethodDef*

SourceFileDef <- SOURCE_keyword newline

## Classes

ClassDef <- CLASS_keyword AccessFlag* ClassName newline
InterfaceDef <- INTERFACE_keyword AccessFlag* ClassName newline
AccessFlag <- PUBLIC_keyword / FINAL_keyword 

ClassName <- [a-zA-Z_][a-zA-Z0-9_$/]* skip

SuperDef <- SUPER_keyword ClassName newline

## Fields

FieldDef <- line_comment* FIELD_keyword FieldAccessFlag* FieldName FieldDescriptor (EQ_symbol FieldIniValue)? newline
FieldAccessFlag <- PUBLIC_keyword / PRIVATE_keyword / PROTECTED_keyword / STATIC_keyword / FINAL_keyword / VOLATILE_keyword / TRANSIENT_keyword
FieldName <- [a-zA-Z_][a-zA-Z0-9_$/]* skip
FieldDescriptor <- FieldType
FieldType <- BaseType / ObjectType / ArrayType
BaseType <- 'B' / 'C' / 'D' / 'F' / 'I' / 'J' / 'S' / 'Z'
ObjectType <- 'L' ClassName ';'
ArrayType <- '[' ComponentType
ComponentType <- FieldType
FieldIniValue <- Double / Integer / QuotedString

## Methods

MethodDef <- line_comment*
    METHOD_keyword  MethodAccessFlag* MethodName MethodDescriptor newline
      Instruction*
    END_Keyword METHOD_END_keyword newline

MethodName <- [a-zA-Z_<][a-zA-Z0-9_$/<>]* skip
MethodAccessFlag <- PUBLIC_keyword / PRIVATE_keyword / PROTECTED_keyword / STATIC_keyword / FINAL_keyword / SYNCHRONIZED_keyword / NATIVE_keyword / ABSTRACT_keyword
MethodDescriptor <- LPAREN_symbol ParameterDescriptor* RPAREN_symbol ReturnDescriptor 
ParameterDescriptor <- FieldType
ReturnDescriptor <- FieldType / VoidType
VoidType <- 'V'

Instruction <- line_comment* (Directive / JvmInstruction / Label) line_comment? newline

Directive <- (LIMIT_keyword (StackDirective / LocalDirective) / ThrowsDirective / LineNumberDirective / VarDirective / CatchDirective) newline
StackDirective <-  STACK_keyword Integer
LocalDirective <- LOCAL_keyword Integer
ThrowsDirective <- THROWS_keyword ClassName
LineNumberDirective <- LINE_keyword Integer 
VarDirective <- VAR_keyword Integer IS_keyword VarName FieldDescriptor FROM_keyword Label TO_keyword Label
CatchDirective <- CATCH_keyword ClassName FROM_keyword Label TO_keyword Label USING_keyword Label

VarName <- skip [a-zA-Z_][a-zA-Z0-9_$]* skip

Label <- skip !Keyword [a-zA-Z_][a-zA-Z0-9_$:]* skip

JvmInstruction <- 
    ALOAD_0
  / ALOAD_1
  / ALOAD_2
  / ASTORE_0
  / ASTORE_1
  / ASTORE_2
  / ASTORE_3
  / ATHROW
  / BIPUSH
  / CHECKCAST
  / DUP
  / RETURN
  / GETFIELD
  / GETSTATIC
  / GOTO
  / ICONST_0
  / ICONST_1
  / ICONST_2
  / ICONST_3
  / ILOAD_0
  / ILOAD_1
  / ILOAD_2
  / ILOAD_3
  / INSTANCEOF
  / IRETURN
  / ISTORE_0
  / ISTORE_1
  / ISTORE_2
  / ISTORE_3
  / JSR
  / LDC
  / LOOKUPSWITCH
  / INVOKESPECIAL
  / INVOKESTATIC
  / INVOKEVIRTUAL
  / MONITORENTER
  / MONITOREXIT
  / NEW
  / POP
  / RET
  / RETURN
  / SWAP
  / TABLESWITCH

Integer <- skip [0-9][0-9]* skip
Double <- skip [0-9]* DOT_symbol [0-9]+ skip
QuotedString <- skip DOUBLE_QUOTE_symbol [^\"]* DOUBLE_QUOTE_symbol skip


## JVM Instructions

ALOAD_0        <-  skip  'aload_0'        skip
ALOAD_1        <-  skip  'aload_1'        skip
ALOAD_2        <-  skip  'aload_2'        skip
ASTORE_0       <-  skip  'astore_0'       skip
ASTORE_1       <-  skip  'astore_1'       skip
ASTORE_2       <-  skip  'astore_2'       skip
ASTORE_3       <-  skip  'astore_3'       skip
ATHROW         <-  skip  'athrow'         skip
BIPUSH         <-  skip  'bipush'         skip       Integer
CHECKCAST      <-  skip  'checkcast'      skip       ClassName
DUP            <-  skip  'dup'            skip
GETFIELD       <-  skip  'getfield'       FieldName  FieldDescriptor
GETSTATIC      <-  skip  'getstatic'      skip       FieldName        FieldDescriptor
GOTO           <-  skip  'goto'           skip       Label
ICONST_0       <-  skip  'iconst_0'       skip
ICONST_1       <-  skip  'iconst_1'       skip
ICONST_2       <-  skip  'iconst_2'       skip
ICONST_3       <-  skip  'iconst_3'       skip
ILOAD_0        <-  skip  'iload_0'        skip
ILOAD_1        <-  skip  'iload_1'        skip
ILOAD_2        <-  skip  'iload_2'        skip
ILOAD_3        <-  skip  'iload_3'        skip
INSTANCEOF     <-  skip  'instanceof'     skip       ClassName
INVOKESPECIAL  <-  skip  'invokespecial'  skip       MethodName       MethodDescriptor
INVOKESTATIC   <-  skip  'invokestatic'   skip       MethodName       MethodDescriptor
INVOKEVIRTUAL  <-  skip  'invokevirtual'  skip       MethodName       MethodDescriptor
IRETURN        <-  skip  'ireturn'        skip
ISTORE_0       <-  skip  'istore_0'       skip
ISTORE_1       <-  skip  'istore_1'       skip
ISTORE_2       <-  skip  'istore_2'       skip
ISTORE_3       <-  skip  'istore_3'       skip
JSR            <-  skip  'jsr'            skip       Label
LDC            <-  skip  'ldc'            skip       QuotedString
MONITORENTER   <-  skip  'monitorenter'   skip
MONITOREXIT    <-  skip  'monitorexit'    skip
NEW            <-  skip  'new'            skip       ClassName
POP            <-  skip  'pop'            skip
RET            <-  skip  'ret'            skip       Integer
RETURN         <-  skip  'return'         skip
SWAP           <-  skip  'swap'           skip

LOOKUPSWITCH <- skip 'lookupswitch' skip newline LookupSwitchPair* DefaultSwitchPair
LookupSwitchPair <- Integer COLON_symbol Label newline
DefaultSwitchPair <- DEFAULT_keyword COLON_symbol Label newline

TABLESWITCH <- skip 'tableswitch' skip Low High newline TableSwitchSingleton* DefaultSwitchPair
Low <- Integer
High <- Integer
TableSwitchSingleton <- Label newline


## Keywords

ABSTRACT_keyword      <-  skip  'abstract'      skip
CATCH_keyword         <-  skip  '.catch'        skip
CLASS_keyword         <-  skip  '.class'        skip
DEFAULT_keyword       <-  skip  'default'       skip
END_Keyword           <-  skip  '.end'          skip
FIELD_keyword         <-  skip  '.field'        skip
FINAL_keyword         <-  skip  'final'         skip
FROM_keyword          <-  skip  'from'          skip
INTERFACE_keyword     <-  skip  '.interface'    skip
IS_keyword <- skip 'is' skip
LIMIT_keyword         <-  skip  '.limit'        skip
LINE_keyword <- skip '.line' skip
LOCAL_keyword         <-  skip  'locals'        skip
METHOD_END_keyword    <-  skip  'method'        skip
METHOD_keyword        <-  skip  '.method'       skip
NATIVE_keyword        <-  skip  'native'        skip
PRIVATE_keyword       <-  skip  'private'       skip
PROTECTED_keyword     <-  skip  'protected'     skip
PUBLIC_keyword        <-  skip  'public'        skip
SOURCE_keyword        <-  skip  '.source'       skip  FileName
STACK_keyword         <-  skip  'stack'         skip
STATIC_keyword        <-  skip  'static'        skip
SUPER_keyword         <-  skip  '.super'        skip
SYNCHRONIZED_keyword  <-  skip  'synchronized'  skip
THROWS_keyword        <-  skip  '.throws'       skip
TO_keyword            <-  skip  'to'            skip
TRANSIENT_keyword     <-  skip  'transient'     skip
USING_keyword         <-  skip  'using'         skip
VAR_keyword <- skip '.var' skip
VOLATILE_keyword      <-  skip  'volatile'      skip

Keyword <- ABSTRACT_keyword / CLASS_keyword / END_Keyword / FIELD_keyword / FINAL_keyword / LIMIT_keyword / LOCAL_keyword 
          / METHOD_END_keyword / METHOD_keyword / NATIVE_keyword / PRIVATE_keyword / PROTECTED_keyword 
          / PUBLIC_keyword / STACK_keyword / STATIC_keyword / SYNCHRONIZED_keyword / THROWS_keyword / SUPER_keyword 
          / TRANSIENT_keyword / VOLATILE_keyword / CATCH_keyword / FROM_keyword / TO_keyword / USING_keyword / DEFAULT_keyword
          / SOURCE_keyword / INTERFACE_keyword / LINE_keyword / VAR_keyword / IS_keyword


## Symbols

COLON_symbol         <-  skip  ':'  skip
DOT_symbol           <-  skip  '.'  skip
DOUBLE_QUOTE_symbol  <-  skip  '"'  skip
EQ_symbol            <-  skip  '='  skip
LPAREN_symbol        <-  skip  '('  skip
RPAREN_symbol        <-  skip  ')'  skip
SEMI_COLON_symbol    <-  skip  ';'  skip

FileName <- skip [a-zA-Z_][a-zA-Z0-9_.]* skip

line_comment <- SEMI_COLON_symbol [^\n]* newline
skip <- [ \t]*
newline <- [\n]*
eof <- !.